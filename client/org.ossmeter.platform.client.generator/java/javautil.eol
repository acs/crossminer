import "../util/ecoreutil.eol";


operation OSS!EClass getFullyQualifiedSubclasses() : Sequence {
	var res = new Sequence;							
	for (c in OSS!EClass.all.select(s | not s.isIgnored())) {
		
		for (s in c.eAllSuperTypes) {
			if (s.isAnnotatedAs("stub")) {
				var stub = s.getAnnotationValue("stub", "javaClass");
				if (stub == self.ePackage.name+"."+self.name) {
					res.add(c);
					break;
				}
			}
		}
	}
	return res;
}

operation OSS!EClass getAbstractModifier() {
	if (self.abstract) return " abstract";
	else return "";
}

operation OSS!EClass getJavaExtends() {
	if (self.eSuperTypes.size() > 0) {
		return self.eSuperTypes.get(0).getJavaName();
	}
	else {
		return "Object";
	}
}

operation OSS!EClassifier getJavaBoxedName() {
	if (self.isAnnotatedAs("stub")) {
		return self.getAnnotationValue("stub", "javaClass");
	}
	else {
		switch (self.name) {
			case "EString": return "String";
			case "ELong": return "Long";
			case "EInt": return "Integer";
			case "EBoolean": return "Boolean";
			case "EFloat": return "Float";
			case "EIntegerObject": return "Integer";
			case "EBooleanObject": return "Boolean";
			case "EDoubleObject": return "Double";
			default: return self.name;
		}
	}
}

/* This is a workaround for this _bug_: https://www.eclipse.org/forums/index.php/t/809707/ */
operation String getJavaBoxedName(parent : Any) {
	if (parent.isTypeOf(OSS!EClassifier) and parent.isAnnotatedAs("stub")) {
		return parent.getAnnotationValue("stub", "javaClass");
	}
	else {
		switch (self) {
			case "EString": return "String";
			case "ELong": return "Long";
			case "EInt": return "Integer";
			case "EBoolean": return "Boolean";
			case "EFloat": return "Float";
			case "EIntegerObject": return "Integer";
			case "EBooleanObject": return "Boolean";
			case "EDoubleObject": return "Double";
			default: return self;
		}
	}
}

operation OSS!EReference getJavaCollectionType() {
	var stubClass = self.eType.getAnnotationValue("stub", "javaClass");
	if (stubClass.isDefined() and self.getAnnotationValue("collection", "fromstub") == "true") {
		return stubClass + "Collection";
	}
	else {
		return self.eType.name + "Collection";
	}
}

operation OSS!EReference getMongoCollectionName() {
	if (self.eContainer().getAnnotationValue("db", "qualifiedCollectionNames") == "true") {
		return self.eContainer().name + "." + self.name;
	} 
	else {
		return self.name;
	}
}

operation OSS!EClassifier isNumeric() : Boolean {
	switch (self.name) {
		case "ELong": return true;
		case "EInt": return true;
		case "EIntegerObject": return true;
		case "EFloat": return true;
		case "EDouble": return true;
		default: return false;
	}
}

operation OSS!EClassifier getJavaName() {
	if (self.isAnnotatedAs("stub")) {
		return self.getAnnotationValue("stub", "javaClass").split("\\.").last();
	}
	else {
		switch (self.name) {
			case "EString": return "String";
			case "ELong": return "long";
			case "EInt": return "int";
			case "EBoolean": return "boolean";
			case "EFloat": return "float";
			case "EIntegerObject": return "int";
			case "EBooleanObject": return "boolean";
			case "EDouble": return "double";
			default: return self.name;
		}
	}
}

/* This is a workaround for this _bug_: https://www.eclipse.org/forums/index.php/t/809707/ */
operation String getJavaName(parent : Any) {
	if (parent.isTypeOf(OSS!EClassifier) and parent.isAnnotatedAs("stub")) {
		return parent.getAnnotationValue("stub", "javaClass");
	}
	else {
		switch (self) {
			case "EString": return "String";
			case "ELong": return "long";
			case "EInt": return "int";
			case "EBoolean": return "boolean";
			case "EFloat": return "float";
			case "EIntegerObject": return "int";
			case "EBooleanObject": return "boolean";
			case "EDouble": return "double";
			default: return self;
		}
	}
}

operation OSS!EClassifier getParseMethod() {
	if (self.isTypeOf(OSS!EEnum)) {
		
	}
	else {
		switch (self.name) {
			case "ELong": return "parseLong";
			case "EInt": return "parseInteger";
			case "EBoolean": return "parseBoolean";
			case "EString": return "parseString";
			case "EFloat": return "parseFloat";
			case "EIntegerObject": return "parseInteger";
			case "EBooleanObject": return "parseBoolean";
			case "EDouble": return "parseDouble";
			default: return "";
		}
	}
}

operation OSS!EAttribute getDefaultJavaValue() {
	if (self.eType.name == "EString") {
		return '"' +  self.defaultValue + '"';
	}
	else if (self.eType.name == "EFloat") {
		return self.defaultValue + "f";
	}
	else if (self.eType.name == "EDouble") {
		return self.defaultValue + "d";
	}
	else if (self.defaultValue.isUndefined()) {
		if (self.eType.getJavaName() == "boolean") return false;
		else if (self.eType.getJavaName() == "int") return 0;
	}
	else if (self.eType.isTypeOf(OSS!EEnum)) {
		return self.eType.name + "." + self.defaultValue;
	}
	else {
		return self.defaultValue;
	}
}